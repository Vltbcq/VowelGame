class_name OptionsPanel
extends Control
## Options : volumes (général, musique, bruitages), plein écran, zoom par défaut, crédits. Émet done(null) en fermant.

signal done(result)

const ZOOMS := [1.0, 1.25, 1.5, 1.75, 2.0]

var groups := {}


func _ready() -> void:
	set_anchors_preset(PRESET_FULL_RECT)
	process_mode = Node.PROCESS_MODE_ALWAYS
	mouse_filter = Control.MOUSE_FILTER_STOP
	UI.fill_bg(self)
	UI.put(self, UI.label("OPTIONS", 30, Pal.ACCENT, HORIZONTAL_ALIGNMENT_CENTER), Vector2(0, 20), Vector2(640, 36))

	var y := 70.0
	for v in [["volume", "Volume général"], ["music_volume", "Musique"], ["sfx_volume", "Bruitages"]]:
		var key: String = v[0]
		UI.put(self, UI.label(v[1], 10, Pal.TEXT), Vector2(150, y + 2))
		var sl := HSlider.new()
		sl.min_value = 0.0
		sl.max_value = 1.0
		sl.step = 0.05
		sl.value = float(Meta.setting(key))
		sl.focus_mode = Control.FOCUS_NONE
		sl.value_changed.connect(func(val): Meta.set_setting(key, val))
		sl.drag_ended.connect(func(_c): Sfx.play("click"))
		UI.put(self, sl, Vector2(280, y), Vector2(200, 16))
		y += 26

	y += 10
	UI.put(self, UI.label("Affichage", 10, Pal.TEXT), Vector2(150, y + 2))
	var wrow := HBoxContainer.new()
	wrow.add_theme_constant_override("separation", 3)
	UI.put(self, wrow, Vector2(280, y), Vector2(300, 16))
	var wbtns := []
	for m in [["fenetre", "Fenêtre"], ["plein", "Plein écran"], ["sans_bord", "Sans bord"]]:
		var mid: String = m[0]
		var b := UI.button(m[1], func():
			Meta.set_setting("window_mode", mid)
			for o in wbtns:
				o.remove_theme_stylebox_override("normal")
				o.remove_theme_color_override("font_color")
				if o.get_meta("mode") == mid:
					UI.selected(o))
		b.set_meta("mode", mid)
		if String(Meta.setting("window_mode")) == mid:
			UI.selected(b)
		wrow.add_child(b)
		wbtns.append(b)
	wrow.get_child(2).tooltip_text = "Plein écran sans bord : Alt+Tab instantané"

	y += 34
	UI.put(self, UI.label("Zoom par défaut", 10, Pal.TEXT), Vector2(150, y + 2))
	_choices("zoom", ZOOMS, Vector2(280, y))
	UI.put(self, UI.label("(en jeu : molette ou + / -)", 10, Pal.DIM), Vector2(280, y + 20))

	# Crédit obligatoire des musiques (licence de soundimage.org)
	y += 44
	UI.put(self, UI.label("Crédits", 10, Pal.TEXT), Vector2(150, y + 2))
	UI.put(self, UI.label("Musique : Eric Matyas · soundimage.org", 10, Pal.DIM), Vector2(280, y + 2), Vector2(300, 12))

	UI.put(self, UI.hotkey(UI.button("Fermer", func(): done.emit(null), 20), [KEY_ESCAPE, KEY_ENTER, KEY_KP_ENTER]), Vector2(260, 316), Vector2(120, 26))


func _choices(key: String, values: Array, pos: Vector2) -> void:
	var row := HBoxContainer.new()
	row.add_theme_constant_override("separation", 3)
	UI.put(self, row, pos, Vector2(300, 16))
	groups[key] = []
	for v in values:
		var val: float = v
		var b := UI.button("×%s" % str(val), func():
			Meta.set_setting(key, val)
			_refresh())
		b.set_meta("v", val)
		row.add_child(b)
		groups[key].append(b)
	_refresh()


func _refresh() -> void:
	for key in groups:
		for b: Button in groups[key]:
			var on := is_equal_approx(float(b.get_meta("v")), float(Meta.setting(key)))
			if on:
				b.add_theme_stylebox_override("normal", UI.sb(UI.SELECTED, Pal.ACCENT, 1))
				b.add_theme_color_override("font_color", Pal.ACCENT)
			else:
				b.remove_theme_stylebox_override("normal")
				b.remove_theme_color_override("font_color")
